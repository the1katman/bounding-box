plugins {
    id 'application'
}

java {
    def javaVersion = JavaVersion.VERSION_21
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

compileJava {
    // encoding
    options.encoding = 'UTF-8'

    // forking
    options.fork = true

    // linting
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'

    // use sun classes with javac
    options.compilerArgs << '-XDignore.symbol.file'
}

application {
    mainClass = 'com.the1katman.BoundingBoxApp'
}

repositories {
    mavenCentral()
}

ext {
    // slf4j
    slf4j_version = '2.0.16'

    // logback
    logback_version = '1.5.8'

    // assertj
    assertj_version = '3.27.3'

    // testng
    testng_version = '7.10.2'

    // test suite
    default_test_suite = 'src/test/resources/testng.xml'
}

dependencies {
    // slf4j
    implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version

    // logback
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: logback_version

    // assertj
    testImplementation group: 'org.assertj', name: 'assertj-core', version: assertj_version

    // testng
    testImplementation group: 'org.testng', name: 'testng', version: testng_version
}

test {
    useTestNG {
        // enable TestNG reporting
        useDefaultListeners = true

        suites default_test_suite
    }

    scanForTestClasses = false

    // prevent Gradle from overwriting the TestNG html report
    reports.html.required = false

    testLogging {
        // set options for log level LIFECYCLE
        events 'failed'
        exceptionFormat = 'short'

        // set options for log level DEBUG
        debug {
            events 'started', 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            exceptionFormat = 'full'
        }
    }

    failFast = true
}
